// To initialize biophysical properties of the cell//
// Reset values, 2008-12-20

//	Parameters  //

    celsius = 34.0                           
                                             
    Rm = 	6e4	 //2.8e5		-	                           
    RmEnd = 2e4		 //3.2e4                              
    RmSoma = Rm                              
    RmAx = Rm                                
                                             
    Cm    = 1       	 // -                 
                                        
    RaAll = 80 		 // +                       
                                             
    Vrest = -70
    
    gna = 0.03		//0.01 			// +   
     
    gkdr = 0.005	//0.006		                     
     
    gka = 0.005		//0.0075                                       
    gka_dist = 	gka	  	              
    gka_prox =  gka	  	  
              
    ghd = 2e-5 		  //		
    gh_soma	=	ghd
    gh_end	=	10*ghd
    
    gabab_gabaa_ratio = 0.6
    nmda_ampa_ratio = 0.6		//to be reset in the main() function
    Block_NMDA_Flag = 0
    
// To balance current //    

proc current_balance() {

 		forall {
 		
        if (ismembrane("na3") || ismembrane("nax")) {ena=55} 
        if (ismembrane("kap") || ismembrane("kad") || ismembrane("kdr")) {ek=-90}
        if (ismembrane("hd") ) {ehd_hd=-30}
		}
	
		finitialize($1)
  	fcurrent()

  	forall {
				for (x) {	
					if (ismembrane("na_ion")||ismembrane("k_ion")){e_pas(x)=v(x)+(ina(x)+ik(x))/g_pas(x)}
					if (ismembrane("hd")) {e_pas(x)=e_pas(x)+i_hd(x)/g_pas(x)}	
				}
				fcurrent()	
		}
//  	printf("Balancing each segment to %d mV\n", $1)		
}			
    
// cell initialization //
    
proc init() {
		
		access soma
		
		distance()

		forsec "soma" {	
		
			insert na3  gbar_na3 = gna
			
      insert kdr  gkdrbar_kdr = gkdr
      
      insert hd 	ghdbar_hd = gh_soma                                   
                  vhalfl_hd = -73         
                                            
      insert kap  gkabar_kap = gka_prox       
                             
      insert pas  e_pas = Vrest g_pas = 1/RmSoma Ra = RaAll cm = Cm 
      
    }
      
    forsec axon_sections	{
    
			insert nax  gbar_nax = 2*gna
			
      insert kdr  gkdrbar_kdr = gkdr
      
      insert kap  gkabar_kap = gka_prox  
	                                                 
      insert pas  e_pas = Vrest g_pas = 1/RmAx Ra = RaAll cm = Cm                      

    } 
      
    forsec basal_dendrite	{
    
			insert na3  gbar_na3 = gna
			
      insert kdr  gkdrbar_kdr = gkdr
	           
      insert kap gkabar_kap = gka_prox
      
      insert pas e_pas = Vrest g_pas = 1/Rm Ra = RaAll cm = Cm     
    }
    
    forsec apical_dendrite {
    
    	insert pas e_pas = Vrest Ra = RaAll  cm = Cm  
		                                                    
			insert na3  gbar_na3 = gna
			
      insert kdr  gkdrbar_kdr = gkdr
	           
//	   if (diam>0.5 && distance(0.5)<500) {         
	    insert hd                                                    
		  insert kap                                                               
		  insert kad                                                             
                                                                              
        for (x) if (x>0 && x<1) { 
        
        					xdist = distance(x)
        					                  
                  rm = RmSoma + (RmEnd - RmSoma)/(1.0 + exp((300-xdist)/50))  
         					g_pas(x) = 1.0/rm 
         					         					        					                                           
                  ghdbar_hd(x) = gh_soma + (gh_end - gh_soma)/(1.0 + exp((300-xdist)/50))	//7 fold when xdist=335
                  if (xdist < 100 ) {
          								 vhalfl_hd = -73
          				} else { vhalfl_hd = -81 }
          				 
                  if (xdist > 300) { 
                  	ar2_na3(x) = 0.3 
                	} else {               
               				ar2_na3(x) = 0.9 - xdist*(0.9-0.3)/300
                		}
                			
                	if (xdist < 100 ) {
          						gkabar_kad(x) = 0
          						gkabar_kap(x) = gka_prox*(1+xdist/70)
       						} else if (xdist < 350){
         							 gkabar_kap(x) = 0
         							 gkabar_kad(x) = gka_dist*(1+xdist/70)	//6 fold when xdist=350
      							} else {
      								gkabar_kap(x) = 0
         							gkabar_kad(x) = gka_dist*6
											}
        }                                                      
    } 
//   } 
		current_balance(Vrest)   
//		print "Cell Initialized!\n"	  
}


                                                                          
